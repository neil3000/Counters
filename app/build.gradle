plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "rahmouni.neil.counters"
        minSdk 21
        targetSdk 31
        versionCode 34
        versionName "1.17.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    flavorDimensions "api"
    productFlavors {
        create("minApi27") {
            dimension = "api"
            minSdk = 27
            versionCode = 270000 + (android.defaultConfig.versionCode ?: 0)
            versionNameSuffix = ".00.27"
        }
        create("minApi21") {
            dimension = "api"
            minSdk = 21
            versionCode = 210000 + (android.defaultConfig.versionCode ?: 0)
            versionNameSuffix = ".00.21"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xjvm-default=all", "-Xopt-in=kotlin.RequiresOptIn"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.0.5"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'rahmouni.neil.counters'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.activity:activity-ktx:1.4.0"

    implementation platform('com.google.firebase:firebase-bom:30.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'

    implementation "androidx.compose.ui:ui:1.2.0-beta02"
    // Tooling support (Previews, etc.)
    debugImplementation("androidx.compose.ui:ui-tooling:1.1.1")
    implementation "androidx.compose.ui:ui-tooling-preview:1.1.1"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation("androidx.compose.foundation:foundation:1.2.0-beta02")
    // Material Design
    implementation("androidx.compose.material:material:1.1.1")
    implementation("androidx.compose.material3:material3:1.0.0-alpha12")
    implementation("androidx.compose.material3:material3-window-size-class:1.0.0-alpha12")
    // Material design icons
    implementation("androidx.compose.material:material-icons-core:1.1.1")
    implementation("androidx.compose.material:material-icons-extended:1.1.1")

    implementation 'androidx.activity:activity-compose:1.4.0'

    implementation "com.google.accompanist:accompanist-insets-ui:0.15.0"

    // Room components
    implementation "androidx.room:room-ktx:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"
    androidTestImplementation "androidx.room:room-testing:2.4.2"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0-rc01"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation "androidx.compose.runtime:runtime-livedata:1.1.1"


    // Kotlin components
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"

    // UI
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.6.0"

    // Testing
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.1.1")
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    debugImplementation("androidx.compose.ui:ui-test-manifest:1.1.1")
    androidTestImplementation ("androidx.test.espresso:espresso-core:3.4.0", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

    implementation("androidx.navigation:navigation-compose:2.4.2")

    implementation "androidx.browser:browser:1.4.0"

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'

    //Health & Connect

    minApi21CompileOnly 'androidx.health:health-connect-client:1.0.0-alpha01'
    minApi27Implementation 'androidx.health:health-connect-client:1.0.0-alpha01'
}